def libraryVersion = '0.0.1'
def siteUrl = 'https://github.com/cs-victor-nascimento/canarinho'
def gitUrl = 'https://github.com/cs-victor-nascimento/canarinho.git'
def group = 'br.com.concretesolutions'
def artifact = 'canarinho'
def libName = 'Android Canarinho'
def libDescription = 'Utilitários Android para padrões Brasileiros'

apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

project.version = libraryVersion

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {

                groupId = group
                artifactId = artifact
                version = libraryVersion

                name libName
                description libDescription
                url = siteUrl

                inceptionYear '2015'

                issueManagement {
                    system 'Github Issues'
                    url 'https://github.com/cs-victor-nascimento/canarinho/issues'
                }

                scm {
                    url siteUrl
                    connection gitUrl
                    developerConnection gitUrl
                }

                organization {
                    name 'Concrete Solutions'
                    url 'http://www.concretesolutions.com.br'
                }

                developers {

                    developer {
                        id 'cs-victor-nascimento'
                        name 'Victor Oliveira Nascimento'
                    }
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

            }
        }
    }
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

task install(dependsOn: uploadArchives)

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")

    dryRun = false
    publish = true

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = artifact
        desc = libDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true

        version {
            name = libraryVersion
            vcsTag = libraryVersion
        }
    }
}
